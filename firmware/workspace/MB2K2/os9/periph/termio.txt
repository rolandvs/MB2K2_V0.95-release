 nam termio
 ttl console IO via MON09

 use defsfile

Edition  equ 1
Revision equ 1
*
 org V.SCF
iport  rmb 1
oport  rmb 1
flash  rmb 2
PST    equ .
*
 mod TEND,TNAM,Drivr+Objct,Reent+Revision,TENT,PST
 fcb READ.+WRITE.
TNAM fcs /termio/
 fcb Edition

TENT lbra INIT
 lbra READ
 lbra WRITE
 lbra GETSTA
 lbra PUTSTA
 lbra TERM

**********************
* INIT
*  
* Get iport and oport from DIP switches.
INIT 	lda SYSREG
		coma
		lsra
		lsra
		lsra
		lsra
		tfr		a,b
		andb	#1
		stb 	iport,u
		tfr		a,b
		lsrb
		andb	#1
		stb 	oport,u
		clrb
      	rts


**********************
* READ
*
READ 	pshs 	d,x
		tst		oport,u	
		bne		flash1
		ldx 	flash,u
		bne		flash2
  		bsr     curs
  		ldx		#2000
flash2	leax	-1,x
		stx 	flash,u
flash1	ldx		#$F1B7	STATAB
		ldb		iport,u
		aslb
 		jsr 	[b,x]
		puls	d,x		
 		bne 	readbyte
 		pshs 	x
 		ldx 	#1
 		os9 	f$sleep
 		puls 	x
 		bra 	read
readbyte pshs 	b,x
 		bsr     curs
rd11 	ldx 	#$F1AB 	INTAB
 		ldb 	iport,u
 		aslb
 		jsr 	[b,x]
 		anda 	#$7F
 		puls b,x
wrt 	      clrb 
 		rts

**********************
* WRITE
*
WRITE pshs b,x
 ldx #$F1B1 OUTTAB
 ldb oport,u
 aslb
 jsr [b,x]
 puls b,x
 clrb
 rts
*
* Invert cursor at current position
curs pshs d
 ldb #$D5 toggle cursor
 stb $FF15
 puls d,pc
*
**********************
* GETSTA
*  Data ready - C bit clear, (B) = 0
*     No data - C bit set,   (B) = $F6 (E$NRDY)
*
GETSTA cmpa #1
 beq check
 cmpa #6
 beq wrt
 comb
 ldb #E$UnkSVC
 rts
*
check   pshs   d,x 
        tst    oport,u
        bne    flash11
        ldx    flash,u
        bne    flash12
        bsr    curs
        ldx    #2000
flash12 leax   -1,x
        stx    flash,u
flash11 ldx    #$F1B7    STATAB
        ldb    iport,u
        aslb
        jsr    [b,x] 
        puls   d,x
        bne    wrt    (Z)=0 if character ready.
*
        comb
        ldb    #E$NotRdy
        rts

**********************
* PUTSTA
*   No status requests
*
PUTSTA comb
 ldb #E$UnkSVC
 rts

**********************
* TERM
*   Terminate Driver
*
TERM rts

 emod
TEND equ * 

**************************
*  TERMINAL device descriptor module

 mod TrmEnd,TrmNam,DEVIC+OBJCT,REENT+1,TrmMgr,TrmDrv
 fcb UPDAT. mode
 fcb $0 port bank
 fdb $FF08 port address
 fcb TrmNam-*-1 option byte count
 fcb DT.SCF Device Type: SCF

* DEFAULT PARAMETERS

 fcb 0 case=UPPER and lower
 fcb 1 backspace=BS,SP,BS
 fcb 0 delete=backspace over line
 fcb 1 auto echo on
 fcb 1 auto line feed on
 fcb 0 null count
 fcb 0 end of page pause on
 fcb 24 lines per page
 fcb C$BSP backspace char
 fcb C$DEL delete line char
 fcb C$CR end of record char
 fcb C$EOF end of file char
 fcb C$RPRT reprint line char
 fcb C$RPET dup last line char
 fcb C$PAUS pause char
 fcb C$INTR Keyboard Interrupt char
 fcb $11 Keyboard Quit char
 fcb C$BSP backspace echo char
 fcb C$BELL line overflow char
 fcb 0 reserved
 fcb 0 reserved
 fdb TrmNam offset of echo device
 fcb 0 Transmit Enable char
 fcb 0 Transmit Disable char
TrmNam fcs "term" device name
TrmMgr fcs "SCF"  file manager
TrmDrv fcs "termio" device driver

 emod Module CRC

TrmEnd EQU *
 
