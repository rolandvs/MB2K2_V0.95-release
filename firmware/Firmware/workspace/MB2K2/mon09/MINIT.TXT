KEYREG EQU $FF00
PIACA EQU $FF01
SYSREG EQU $FF02
PIACB EQU $FF03
*
ACIAD1 EQU $FF08
ACIAC1 EQU $FF09
ACIAD2 EQU $FF04
ACIAC2 EQU $FF05
BAUD1 EQU $FF0C
BAUD2 EQU $FF0D
*
GDCPRM EQU $FF14
GDCCOM EQU $FF15
*
RTCADD EQU $FF18
RTCDAT EQU $FF19
*
PORTA EQU $FF1C
PORTB EQU $FF1D
PORTC EQU $FF1E
BITCON EQU $FF1F
*
HDR FCC $0A,$0D,'+++ Mon09 Ver 6.2 (FLEX+OS9L1) D.A.Rumball 2023 +++',4
PROMPT FCC $0A,$0D,'=>',4
NOTTHS FCC 7,' Unknown command! ',4
UNMESS FCC 7,'UNEXPECTED INTERUPT!',4
FAILMS FCC 7,7,7,'Power failure in RTC, reloading defaults',4
*
RTCTAB FCB %00001111 no 2Mhz, no 256k, 128k Promdisk, 30ms step rate.
 FCB $AA powerfail flag
 FCB 0,1,2,$FF promdisk, ramdisk, f-ramdisk, unassigned
 FCB $08,0,$3A,$18,$50,0,0,$08,0,0,$1B TTYSET parameters.
 FCB 0,1 ASN parameters.
 FCB $00,$00,$00,$00,$00,$00,$00,$00 GDC timing parameters
 FCB 0,0,0,0,0,0,0 reserved
 FCB $00,$11,$22,$33,$44,$55,$66,$77,$88,$99,$AA,$BB,$CC,$DD,$EE,$FF user params
 FCB 7,20,7,19 (Sat 20th July 2019, MB2K2's birthday!)
 *
MINIT STX RNDM seed random number generator.
* Check that RTC data is ok.
 LDB #$0D
 JSR GETRTC
 ANDA #$80
 STA RTCFAIL
* Get iport and oport.
 LDA SYSREG
 COMA
 RPT 4
 LSRA
 TFR A,B
 ANDB #$01
 STB IPORT
 TFR A,B
 LSRB
 ANDB #$01
 STB OPORT
* Set up rtc defaults if
* there has been a power failure.
 TST RTCFAIL
 BNE SUD
 LDX #RTCTAB
 LDB #$0E
RTCLOOP LDA ,X+
 JSR PUTRTC
 INCB
 CMPB #$40
 BNE RTCLOOP
* set a default date 
 LDB #06
RTCLP1 LDA ,X+
 JSR PUTRTC
 INCB
 CMPB #$0A
 BNE RTCLP1
*
* Set up drive step rate.
SUD LDB #$0E
 JSR GETRTC
 TFR A,B
*
 TFR B,A
 ANDA #$0C
 LSRA
 LSRA
 STA STEP0
*
* Reset fdc.
 CLRA
 ADDA STEP0
 STA COMREG
*
* Set up gdc.
SUGDC JSR GDCINIT
* INC CFLAG enable text cursor
* Set up initial drive type table.
 LDB #$10
 LDX #DDSTAB
F1A JSR GETRTC
 STA ,X+
 INCB
 CMPB #$14
 BNE F1A
*
 TST RTCFAIL
 BNE CONT
 LDX #FAILMS
 JSR PSTRNG
 LDA #$80
 STA RTCFAIL
CONT LDA SYSREG check for autoboot and OS choice
 COMA
 anda	#$C0
 cmpa	#$C0 boot OS9
 lbeq   BO
 cmpa	#$80 boot FLEX
 lbeq   BF
*
 JMP MINITR
