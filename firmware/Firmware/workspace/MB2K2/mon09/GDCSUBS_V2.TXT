*
* Send GDC command.
GCOM TSTA If reset then ignore flag.
 BEQ GCOM2
 PSHS A
GCOM1 LDA GDCPRM
 BITA #$04
 BEQ GCOM1
 PULS A
GCOM2 STA GDCCOM
 RTS
*
*
* Send GDC parameter.
GPRM PSHS A
GPRM1 LDA GDCPRM
 BITA #$04
 BEQ GPRM1
 PULS A
 STA GDCPRM
 RTS
*
*
* Read parameter byte from GDC.
GPRMI LDA GDCPRM
 BITA #$01 Data ready?
 BEQ GPRMI
 LDA GDCCOM
 RTS
*
*
* Set mask.
MASK PSHS D
 LDA #$4A
 BSR GCOM
 TFR X,D
 EXG A,B
 BSR GPRM
 EXG A,B
 BSR GPRM
 PULS D,PC
*
*
* Define line profile and 'pen' type.
SETPEN PSHS D,X
 ANDA #$03
 ORA #$20
 BSR GCOM
 TFR X,D
 STB GPARAM+1
 STA GPARAM
 LDX #GPARAM-6
 BSR SETPAT
 PULS D,X,PC
*
*
* Set up graphics pattern in parameter ram.
* X points to eight bytes in ram.
SETPAT PSHS A,B,X
 LDA #$78
 BSR GCOM
 LDB #8
 LEAX 8,X
SETPT1 LDA ,-X
 BSR GPRM
 DECB
 BNE SETPT1
 PULS A,B,X,PC
*
*
* Set up figs command using parameter set in ram
FIGSF PSHS A,B,X
 LDA #$4C
 BSR GCOM
 LDX #GFIGS
 LDA ,X
 BSR GPRM
 DECB
 BEQ FIN
FIGSF1 LDA 2,X
 BSR GPRM
 LDA 1,X
 LEAX 2,X
 BSR GPRM
 DECB
 DECB
 BNE FIGSF1
FIN LDA #$6C
 LBSR GCOM
 PULS A,B,X,PC
*
*
* Set figs command for graphics.
FIGSG PSHS A,B,X
 LDA #$4C
 LBSR GCOM
 LDX #GFIGS
 LDA ,X
 LBSR GPRM
 DECB
 BEQ FING
FIGSG1 LDA 2,X
 LBSR GPRM
 LDA 1,X
 LEAX 2,X
 LBSR GPRM
 DECB
 DECB
 BNE FIGSG1
FING LDA #$68
 LBSR GCOM
 PULS A,B,X,PC
*
*
* Set up partitions in GDC.
SETPAR PSHS A,B,X,Y,U
 EXG A,B
 STD PART2
 TFR X,D
 EXG A,B
 STD PART1
 TFR Y,D
 ANDB #$0F
 LDA #16
 MUL
 STB PART1+2
 TFR Y,D
 LSRD
 LSRD
 LSRD
 LSRD
 STB PART1+3
 TFR U,D
 ANDB #$0F
 LDA #16
 MUL
 STB PART2+2
 TFR U,D
 LSRD
 LSRD
 LSRD
 LSRD
 STB PART2+3
* BSR SYNC
 LDA #$70
 LBSR GCOM
 LDX #PART1
 LDB #8
SETPR1 LDA ,X+
 LBSR GPRM
 DECB
 BNE SETPR1
 PULS A,B,X,Y,U,PC
*
*
SYNC PSHS A
SY1 LDA GDCPRM
 BITA #$20
 BNE SY1
SY2 LDA GDCPRM
 BITA #$20
 BEQ SY2
 PULS A,PC
*
*
* Set graphics cursor (modified version that uses the 'secret' GDC command $D7).
SETCRG PSHS A,B,X,Y
 STX XCOORD
 STY YCOORD
 LDA #$D7
 STA GDCCOM
 TFR X,D send x,y lo byte first
 STB GDCPRM
 STA GDCPRM
 TFR Y,D
 STB GDCPRM
 STA GDCPRM
 PULS A,B,X,Y,PC
*
*
* Read graphics cursor.
GETCRG PSHS A,B
 LDA #$E0
 LBSR GCOM
 LBSR GPRMI LOW BYTE
 TFR A,B
 LBSR GPRMI HIGH BYTE.
 LDY #0
 SUBD #$5E00
GTCRG1 SUBD #48
 BMI GTCRG2
 LEAY 1,Y
 BRA GTCRG1
GTCRG2 ADDD #48
 ASLD
 ASLD
 ASLD
 ASLD
 TFR D,X
 LBSR GPRMI
 LBSR GPRMI GET FIRST MASK BYTE
 TFR A,B
 LBSR GPRMI
 CLC
GTCRG3 LSRD
 BCS GTCRG4
 LEAX 1,X
 BRA GTCRG3
GTCRG4 STX XCOORD
 STY YCOORD
 PULS A,B,PC
*
* Switch off display.
OFF PSHS A
 JSR SYNC
 LDA #$0C
 LBSR GCOM
 PULS A,PC
*
*
* Switch on display.
ON PSHS A
 JSR SYNC
 LDA #$0D
 LBSR GCOM
 PULS A,PC
*
*
* Set display to graphics.
GRAPH PSHS D,X,Y,U
 CLRB
 BSR MODE
 ORA #$09
 DECB
 BSR MODE
 LDX #$5E00
 LDY #576
 LDD #0
 TFR D,U
 JSR SETPAR
 PULS D,X,Y,U,PC
*
* Set GDC mode.
MODE TSTB
 BEQ MODE1
 STA GMODE
 PSHS A
 LDA #$0F
 LBSR GCOM
 PULS A
 LBSR GPRM
 RTS
MODE1 LDA GMODE
 RTS
*
*
* Set graphics zoom.
ZOOM TSTB
 BEQ ZOOM1
 STA GZOOM
 PSHS A
 LDA #$46
 LBSR GCOM
 PULS A
 LBSR GPRM
 RTS
ZOOM1 LDA GZOOM
 RTS
*
*
* Area fill.
FILL PSHS A,B,Y,X
 ANDA #$87
 ORA #$10
 STA GFIGS
 LEAY -1,Y
 TFR Y,D
 STD DC
 TFR X,D
 STD D0
 STD D2
 LDB #7
 LBSR FIGSG
 PULS A,B,Y,X,PC
*
*Clear graphics screen.
CLEARG PSHS D,X,Y
 LDX #0
 TFR X,Y
 JSR SETCRG
 LDX #16384
 LDA #2
 BSR CLEARX
 LDX #(48*577-16384)
 LDA #2
 BSR CLEARX
 LDX #0
 TFR X,Y
 JSR SETCRG
 PULS D,X,Y,PC
*
CLEARX PSHS A,X
 PSHS A
 LEAX -1,X
 PSHS X
 LDX #$FFFF
 JSR MASK
 LDA #$4C
 LBSR GCOM
 LDA #2
 LBSR GPRM
 PULS D
 EXG A,B
 LBSR GPRM
 EXG A,B
 LBSR GPRM
 PULS A
 ORA #$20
 LBSR GCOM
 LDA #$FF
 LBSR GPRM
 LBSR GPRM
 PULS A,X,PC
*
*
* Init display parameters.
GDCINIT PSHS A,B,X
* Init gdc
 CLRA
 LBSR GCOM
* Set up main sync parameters.
 LDB #$21
GDC3 JSR GETRTC
 JSR GPRM
 INCB
 CMPB #$29
 BNE GDC3
* Master vsync
SUGDC1 LDA #$6F
 LBSR GCOM
* Define pitch.
 LDA #$47
 LBSR GCOM
 LDA #$30
 LBSR GPRM
* Define cursor.
 LDA #$4B
 LBSR GCOM
 CLRA
 LBSR GPRM
 LDA #$C0
 LBSR GPRM
 CLRA
 LBSR GPRM
* Set zoom.
 LDB #1
 CLRA
 LBSR ZOOM
* Set text mode.
 LDA #$16
 STA GMODE
* Switch on display.
 LDA #$6B
 LBSR GCOM
 JSR CLEART
 JSR TEXT
 *
 LDA #$0C init the GDC terminal emulator
 JSR GDCOUT
*
 PULS A,B,X,PC
*
* Plot a point.
POINT PSHS B
 CLR GFIGS
 LDB #1
 LBSR FIGSF
 PULS B,PC
*
*
* Plot a line.
LINE PSHS D
 LDA #$D8
 STA GDCCOM
 TFR X,D send x,y lo byte first
 STB GDCPRM
 STA GDCPRM
 TFR Y,D
 STB GDCPRM
 STA GDCPRM 
 PULS D
 STX XCOORD   UPDATE COORDINATES
 STY YCOORD
 LBSR SETCRG
 RTS
TABLE FCB $09,$08,$0A,$0B,$0E,$0F,$0D,$0C
*
* Plot a rectangle.
RECT PSHS A,B,X,Y
 ANDA #$07
 ORA #$40
 STA GFIGS
 LDD #$0003
 STD DC
 LEAX -1,X
 LEAY -1,Y
 TFR X,D
 STD D0
 STD DM
 TFR Y,D
 STD D2
 LDD #$FFFF
 STD D1
 LDB #11
 LBSR FIGSF
 PULS A,B,X,Y,PC
*
*
* Plot a circle.
CIRCLE PSHS A,B,X,Y
 LDX XCOORD
 LDY YCOORD
 STX XTEMP
 STY YTEMP
 LEAX A,X
 LDB #4
 BSR ARC
 LDB #7
 BSR ARC
 LDX XTEMP
 LEAY A,Y
 LDB #2
 BSR ARC
 LDB #5
 BSR ARC
 LDY YTEMP
 NEGA
 LEAX A,X
 NEGA
 CLRB
 BSR ARC
 LDB #3
 BSR ARC
 LDX XTEMP
 NEGA
 LEAY A,Y
 NEGA
 LDB #1
 BSR ARC
 LDB #6
 BSR ARC
 LDX XTEMP
 LDY YTEMP
 LBSR SETCRG
 PULS A,B,X,Y,PC
*
* Plot an arc.
ARC PSHS A,B
 LBSR SETCRG
 PSHS D
 LDB #181
 MUL
 STA CONST
 PULS D
 ANDB #$07
 ORB #$20
 STB GFIGS
 DECA
 CLRB
 STA D0+1
 STB D0
 EXG A,B
 ASLD
 STD D2
 LDB CONST
 CLRA
 STD DC
 LDD #$FFFF
 STD D1
 LDD #0
 STD DM
 LDB #11
 LBSR FIGSF
 PULS A,B,PC
*
* Set text cursor.
SETCRT PSHS A,B,X
 STX CURSOR
 LDA #$49
 LBSR GCOM
 TFR X,D
 EXG A,B
 LBSR GPRM
 EXG A,B
 LBSR GPRM
 LDA #$08
 LBSR GPRM
 PULS A,B,X,PC
*
* Get text cursor.
GETCRT PSHS A,B
 LDA #$E0
 LBSR GCOM
 LBSR GPRMI
 TFR A,B
 LBSR GPRMI
 TFR D,X
 STX CURSOR
 LBSR GPRMI
 LBSR GPRMI
 LBSR GPRMI
 PULS A,B,PC
*
* Set display to text.
TEXT PSHS A,B,X,Y,U
 CLRB
 LBSR MODE
 ANDA #$F6
 INCB
 LBSR MODE
 LDX TS1
 LDD TS2
 LDY TL1
 LDU TL2
 JSR SETPAR
 PULS A,B,X,Y,U,PC
*
*Clear text screen.
CLEART PSHS D,X
 LDX #$CA00
 BSR SETCRT
 LDX #(48*288)
 LDA #2
 JSR CLEARX
 BSR TXTPAR
 CLR COL
 CLR ROW
 CLR ESCFLG
 LDA #$01
 STA CTYPE
 LDA #4
 STA ATTRI
 BSR TEXT
 TST CFLAG
 BEQ CTE
 BSR CURS
CTE PULS D,X,PC
*
CLINK RTS
*
TXTPAR LDX #$CA00
 TFR X,D
 LDY #288
 LDU #0
TXTPR1 STX TS1
 STD TS2
 STY TL1
 STU TL2
 PSHS A
 LDA GMODE
 BITA #9
 BEQ TXTPR2
 PULS A,PC
TXTPR2 PULS A
 JSR SETPAR
 RTS
*
 LIB GDCOUTV3.TXT
