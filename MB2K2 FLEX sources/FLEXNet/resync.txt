*   File name:  resync.asm
*
*   This utility will try to re-synchronize the communication
*   between the FLEX/netdrv and the remote host computers.
*
*   Vn  01.00   2000-09-23, BjB.
*       02.00   2002-05-01, js, use send/receive vectors
*       02.01   2002-08-31  js Use signature string
*       02.02   2002-09-19  js restore ACIA reset routine
* --------------------------------------------------------------
****************************************************************************************
*   This version is modified for the MB2K system and it's internal drivers for FlexNet. *
*   D.A.Rumball 29-3-2005                                                               *
*************************************************************************************** *
*   FLEX equates.
*
warms   equ     $cd03           FLEX warm start
pstrng  equ     $cd1e           write string to display
pcrlf   equ     $cd24           write cr/lf to display
memend  equ     $cc2b           memory end pointer
*
* ---------------------------------------------------------------
*
SCHAR    EQU  $F0A0     FlexNet driver send char
RCHAR    EQU  $F0A2     FlexNet driver recieve char
*
        org     $c100

resync   bra     start

versn   FCB     1,0             version number
tries   rmb     1               sync tries counter
tmp     rmb     1               temporary storage for sync char
*
* ---------------------------------------------------------------
*
*   Main routine.
*
start   jsr     pcrlf
        lda     #$5             number of tries
        sta     tries
        lda     #$55            1:st sync character

sync    sta     tmp             current sync character

sync04  jsr    [SCHAR]           send character
        bcc     sync16          time-out, report error

        jsr    [RCHAR]           get response
        bcc     sync16          time-out, report error

        cmpa    tmp             received char same as sent?
        beq     sync08          yes
        lda     tmp             1:st sync char?
        cmpa    #$55
        bne     sync20          nope, report error

        dec     tries           decrement try count
        bne     sync04          try again if not = 0
        bra     sync20          report error

sync08  cmpa    #$aa            2:nd sync character?
        beq     sync12          yes, report success
        ldaa    #$aa            send 2:nd sync character
        bra     sync
*
sync12  ldx     #succst         "Connection successfully.."
        bra     sync24

sync16  ldx     #timest         "Time-out.."
        bra     sync24

sync20  ldx     #synest         "Could not sync.."
sync24  jsr     pstrng
        jmp     warms           back to FLEX
*
* ---------------------------------------------------------------
*
succst  fcc     /Connection successfully established/,4
timest  fcc     /Time-out error, connection broken!/,8,4
synest  fcc     /Could not synchronize the communication!/,8,4

*
* ---------------------------------------------------------------
*
*
        end     resync
*
                                                                                                                                                                                                                                                                                                                                                                           